import libc as c;
import libc.{int, strlen, memcmp}
import example.logging as log;
import example.assert.{assert};
import hades.memory as mem;
import example.debug as Debug;

struct t {
    val m_buffer: *Byte;
    val m_length: Size;
}

def from_c_string(cstr: *Byte): t {
    return t(cstr, c.strlen(cstr));
}

const from_c = from_c_string;

extension StringViewExtensions for t {
    
    def buffer(this): *Byte {
        return this.m_buffer;
    }
    
    def length(this): Size {
        return this.m_length;
    }

    def offset(this, offset: Size): Byte {
        assert(offset < this.length()) || b"StringView.offset: Overflow";
        return *mem.pointer_add(this.buffer(), offset);
    }

    def take(this, length: Size): t {
        assert(this.length() >= length) || b"StringView.take(); length overflow";
        return t(this.buffer(), length);
    }

    def drop(this, length: Size): t {
        assert(this.length() >= length) || b"StringView.drop(); length overflow";
        return t(mem.pointer_add(this.buffer(), length), this.length() - length);
    }

    def starts_with_c(this, start: *Byte): Bool {
        return this.starts_with(from_c_string(start));
    }

    def drop_first_word(this): t {
        val mut i: Size = 0;
        while i < this.length() and not is_whitespace(this.offset(i)) {
            i = i + 1;
        }

        while is_whitespace(this.offset(i)) {
            i = i + 1;
        }

        return this.drop(i);
    }

    def drop_words(this, n: Size): t {
        val mut i: Size = 0
        val mut current = this
        while i < n {
            current = current.drop_first_word()
            i = i + 1
        }

        return current
    }

    def take_first_word(this): t {
        val mut i: Size = 0;
        while i < this.length() and not is_whitespace(this.offset(i)) {
            i = i + 1;
        }
        return this.take(i);
    }

    def copy_to_c(this): *mut Byte {
        val buffer = mem.allocate[Byte](this.length() + 1);
        val end_ptr: *mut Byte = mem.mut_pointer_add(buffer, this.length());
        *end_ptr = 0;
        mem.copy(buffer, this.buffer(), this.length());
        return buffer;
    }

    def log(this): Void {
        log.debug(this.buffer());
    }
}

def copy_to_c(self: t): *mut Byte {
    return self.copy_to_c();
}

def is_whitespace(char: Byte): Bool {
    return char == 9 or char == 10 or char == 13 or char == 32;
}

trait ToStringView[Self] {
    def to_string_view(self: Self): t
}

extension ToStringViewExtensions[Self] for Self where ToStringView[Self] {

    def to_string_view(this): t {
        return ToStringView[Self].to_string_view(this)
    }

    def equals[U](this, self: U): Bool  where ToStringView[U] {
        return _equals(self.to_string_view(), this.to_string_view())
    }


    def starts_with[U](this, _start: U): Bool where ToStringView[U] {
        val start = _start.to_string_view()
        val self = this.to_string_view()
        if self.length() < start.length() {
            return false;
        }
        return self.take(start.length()).equals(start);
    }
}


implementation ToStringView[*Byte] {
    def to_string_view(self: *Byte): t {
        return t(self, strlen(self) as Size)
    }
}

implementation ToStringView[t] {
    def to_string_view(self: t): t {
        return self
    }
}

def _equals(self: t, that: t): Bool {
    if self.length() != that.length() {
        return false;
    }
    return memcmp(
        pointer_cast[Void](self.buffer()),
        pointer_cast[Void](that.buffer()),
        self.length()
    ) == 0;
}
